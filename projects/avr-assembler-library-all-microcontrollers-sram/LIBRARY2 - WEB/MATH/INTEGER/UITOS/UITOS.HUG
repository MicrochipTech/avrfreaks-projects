;<$DS_HEADER>
;+--------------------------------------------------------------------------------+              
;|João D´Artagnan A. Oliveira Programmer and Author; 				  |
;|										  |
;| All rights reserved.								  |
;|										  |
;|   Redistribution and use in source and binary forms, with or without		  |
;|  modification, are permitted provided that the following conditions are met:	  |
;|										  |
;|   * Redistributions of source code must retain the above copyright		  |
;|     notice, this list of conditions and the following disclaimer.		  |
;|										  |
;|   THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"  |
;|   AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE    |
;|   IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE   |
;|   ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE     |
;|   LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR	  |
;|   CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF	  |
;|   SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS	  |
;|   INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN	  |
;|   CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)	  |
;|   ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE   |
;|   POSSIBILITY OF SUCH DAMAGE.						  |
;|										  |
;|   Brasília, Brazil November 30,2015 	06:28 am				  |
;+--------------------------------------------------------------------------------+
;</$DS_HEADER>
;+--------------------------------------------------------------+---+-----------+
;|										|
;| DATE:17/12/2001 VERSION 1.0							|
;>------------------------------------------------------------------------------<
;| I N T E G E R  R O U T I N E S	     					|
;| U N S I G N E D  I N T E G E R  T O  S T R I N G  				|
;+------------------------------------------------------------------------------+
;| THIS DRIVE HAS INTEND TO USE WITH AVRS WITH SRAM				|
;+------------------------------------------------------------------------------+
#ifndef	__INTEGER_UITOS__
	#define __INTEGER_UITOS__
	.message "[ (MATH) INTEGER UITOS ]"
;+----------------------------------------------------------------------+
;| _UITOS								|
;| Convert unsigned word to string ASCII 			 	|
;| with left zero removal end 0 terminator				|
;|								  	|
;| Input  : AccH:Acc unsigned word input			  	|
;|	    Z--> SRAM string reserved area when in multitask mode	|
;| Output : Z--> SRAM string output with 0			  	|
;+----------------------------------------------------------------------+
	_UITOS:
		push	Acc					;save useds
		push	AccH
		push	AccT
		push	AccTH
		push	Temp
		#ifdef	__MULTITASK_MODE__
			pushw	Z
		#else
			ldi	ZL,low(_DF_STR)
			ldi	ZH,high(_DF_STR)
		#endif
		ldi	AccT,'+'				;assume positive
		st	Z+,AccT
		ldi	Temp,low(10000)				;load constant
		ldi	AccTH,high(10000)
		call 	_UITOS50				;find 1o digit
		ldi	Temp,low(1000)				;load constant
		ldi	AccTH,high(1000)
		call 	_UITOS50				;find 2o digit
		ldi	Temp,low(100)				;load constant
		ldi	AccTH,high(100)
		call 	_UITOS50				;find 3o digit
		ldi	Temp,low(10)				;load constant
		ldi	AccTH,high(10)
		call 	_UITOS50				;find 4o digit
		ori	Acc,0x30				;convert last digit to ascii
		st	Z+,Acc					;store lst digit
		clr	Acc
		st	Z+,Acc					;end terminated
		#ifdef	__MULTITASK_MODE__
			popw	Z
		#else	
			ldi	ZL,low(_DF_STR)
			ldi	ZH,high(_DF_STR)
		#endif
		ldi	Acc,4
	_UITOS20:
		adiw	ZL,1
		ld	Acch,Z
		cpi	Acch,'0'				;zero ?
		brne	_UITOS30				;branch if not
		dec	Acc
		brne	_UITOS20
		adiw	ZL,1					;put into correct position
	_UITOS30:
		pop	Temp
		pop	AccTH
		pop	AccT
		pop	AccH					;restore useds
	_UITOS40:
		pop	Acc
		ret
	;+---------------------
	;| get digit
	;+---------------------
	_UITOS50:
		ldi	AccT,'0'				;load first digit
	_UITOS60:
		sub	Acc,Temp				;subtract by 10 power factor
		sbc	AccH,AccTH
		brcs	_UITOS70				;found ? branch
		inc	AccT					;digit count
		jmp 	_UITOS60
	_UITOS70:
		st	Z+,AccT					;store ASCII Digit
		add	Acc,Temp				;restore original number
		adc	AccH,AccTH
		ret
;+----------------------------------------------------------------------+
;| _UITOSU								|
;| Convert unsigned word to string ASCII 			 	|
;| fixed size 4 using following format +nnnnn/0	 			|
;|								  	|
;| Input  : AccH:Acc unsigned word input			  	|
;| Output : Z--> SRAM string output with 0			  	|
;+----------------------------------------------------------------------+
	_UITOSU:
		#ifdef	__MULTITASK_MODE__
			pushw	Z
		#else
			ldi	ZL,low(_DF_STR)
			ldi	ZH,high(_DF_STR)
		#endif
		rcall	_UITOS					;*<NOCHANGE>*
		#ifdef	__MULTITASK_MODE__
			popw	Z
		#else	
			ldi	ZL,low(_DF_STR)
			ldi	ZH,high(_DF_STR)
		#endif
		ret
;+----------------------------------------------------------------------+
;| _UITOSUS								|
;| Convert unsigned byte to string ASCII with fixed size 6 chars using 	|
;| following format +nnnnn/0 and fill left zeros with space		|
;|								  	|
;| Input  : Acc unsigned byte input				  	|
;| Output : Z--> SRAM string output with 0			  	|
;+----------------------------------------------------------------------+
	_UITOSUS:
		pushaw
		#ifdef	__MULTITASK_MODE__
			pushw	Z
		#else
			ldi	ZL,low(_DF_STR)
			ldi	ZH,high(_DF_STR)
		#endif
		rcall	_UITOSU					;*<NOCHANGE>*
		ldi	AccH,0					;space count
	_UITOSUS_10:
		adiw	ZH:ZL,1
		ld	Acc,Z
		tst	Acc					;zero terminator?
		breq	_UITOSUS_EXIT				;yes, exit	
		cpi	Acc,'0'					;zero
		brne	_UITOSUS_EXIT				;no, exit
		inc	AccH					;space count++
		cpi	AccH,5
		brsh	_UITOSUS_EXIT
		ldi	Acc,' '					;replace with space
		st	Z,Acc
		jmp 	_UITOSUS_10
	_UITOSUS_EXIT:
		#ifdef	__MULTITASK_MODE__
			popw	Z
		#else	
			ldi	ZL,low(_DF_STR)
			ldi	ZH,high(_DF_STR)
		#endif
		popaw
		ret
#endif
.exit


