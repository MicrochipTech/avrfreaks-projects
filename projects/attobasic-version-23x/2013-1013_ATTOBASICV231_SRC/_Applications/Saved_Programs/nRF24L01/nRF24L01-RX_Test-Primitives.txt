5 REM nRF24L01 RX Test ON Mega2560
10 C:=0 ; SDH5 ; CBH5#CE PIN, SET PH5 TO OUT, SET LOW
15 SPM 0 ; SPW $20 ; SPW $73 ; SPS# POWER UP IN RX MODE, NO IRQ
20 SPW $27 ; SPW $70 ; SPS# CLEAR SPURIOUS IRQ FLAGS
25 SPW $21 ; SPW 0 ; SPS# DISABLE AUTO-ACK
30 SPW $22 ; SPW 1 ; SPS# RX0 enabled
35 SPW $23 ; SPW 3 ; SPS# 5 BYTE ADDR
40 SPW $24 ; SPW 0 ; SPS# DISBLE ARD AND ARC
45 SPW $25 ; SPW 1 ; SPS# CHANNEL 1 @ 2.402GHz
50 SPW $26 ; SPW $0E ; SPS# -18 DBM, 2MBPS
55 SPW $31 ; SPW 4 ; SPS# 4 bytes of data for RX0
60 SPW $E1 ; SPS ; SPW $E2 ; SPS# FLUSH RX AND TX BUFFERS
65 SBH5# ENABLE RX MODE
70 PRI "---------~"# PERFORATION
75 PRI "PAYLOAD # "; PRI C ; C:= C+1# PRINT SOME INFO
80 SPW $17 ; A:= SPR ; SPS ; A:= A AND 1# FETCH THE STATUS OF RX FIFO
85  PRI "." ; SLP 3 ; IF !A THEN GOTO 80# DELAY 0.125S AND CHECK FIFO
90 PRI "~" ; CBH5# ENTER STANDBY TO READ DATA (use CBB1 for M2560)
95 SPW $61; FOR N= 1 4 ; PRI "D:" PRX SPR; NEXT ; SPS# FETCH A BYTE FROM THE RX FIFO
100 GOTO 65# LOOP
