5 REM nRF24L01 TX Test on Mega328
10 SDB1 ; CBB1#CE PIN, SET PB1 TO OUT, SET LOW
15 SPM 0 ; SPW $20 ; SPW $72 ; SPS# POWER UP IN TX MODE, NO IRQ
20 SPW $27 ; SPW $70 ; SPS# CLEAR SPURIOUS IRQ FLAGS
25 SPW $21 ; SPW 0 ; SPS# DISABLE AUTO-ACK
30 SPW $22 ; SPW 1 ; SPS# RX0 enabled
35 SPW $23 ; SPW 3 ; SPS# 5 BYTE ADDR
40 SPW $24 ; SPW 0 ; SPS# DISBLE ARD AND ARC
45 SPW $25 ; SPW 1 ; SPS# CHANNEL 1 @ 2.402GHz
50 SPW $26 ; SPW $0E ; SPS# -18 DBM, 2MBPS
55 SPW $31 ; SPW 4 ; SPS# 4 bytes of data for RX0
60 SPW $E1 ; SPS ; SPW $E2 ; SPS# FLUSH RX AND TX BUFFERS
65 PRI "---------~"# PERFORATION
70 M:=PEEK VPG@RTC3 ; N:=PEEK VPG@RTC2# FETCH UPPER 2 RTC BYTES
75 O:=PEEK VPG@RTC1 ; P:=PEEK VPG@RTC0# FETCH LOWER 2 RTC BYTES
80 SPW $B0 ; SPW M ; SPW N ; SPW O ; SPW P ; SPS# SEND RTC DATA
85 PRI "PAYLOAD~" ; PRI "D:" ; PRX M ; PRI "D:" ; PRX N# SHOW THE TIMESTAMP
90 PRI "D:" ; PRX O ; PRI "D:" ; PRX P# SHOW THE TIMESTAMP
95 SBB1 ; DEL 1 ; CBB1# PULSE CE PIN TO XFER PAYLOAD
100 SLP 6 ; GOTO 65# SLEEP 1 SECOND AND LOOP
